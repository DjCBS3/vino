AC_INIT([vino], [2.23.6],
        [http://bugzilla.gnome.org/enter_bug.cgi?product=vino])
AC_CONFIG_SRCDIR(server/vino-fb.c)

AM_INIT_AUTOMAKE
AM_CONFIG_HEADER(config.h)

AM_MAINTAINER_MODE

dnl make sure we keep ACLOCAL_FLAGS around for maintainer builds to work
AC_SUBST(ACLOCAL_AMFLAGS, "\${ACLOCAL_FLAGS}")

GETTEXT_PACKAGE=vino
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [gettext package])

AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
AC_C_BIGENDIAN

AM_PROG_LIBTOOL
IT_PROG_INTLTOOL([0.35.0])
AC_PATH_PROG(GCONFTOOL, gconftool-2)
AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)

AM_GLIB_GNU_GETTEXT

GNOME_COMPILE_WARNINGS(yes)

AC_ARG_ENABLE(deprecations,
              [AC_HELP_STRING([--disable-deprecations],
                              [don't warn about deprecated usages [default=no]])],,
              [enable_deprecations=yes])

if test "x$enable_deprecations" = "xyes"; then
   DISABLE_DEPRECATED_CFLAGS="\
-DG_DISABLE_DEPRECATED \
-DGDK_DISABLE_DEPRECATED \
-DGTK_DISABLE_DEPRECATED \
-DGDK_PIXBUF_DISABLE_DEPRECATED \
-DGNOME_DISABLE_DEPRECATED"
   AC_SUBST(DISABLE_DEPRECATED_CFLAGS)
fi

PKG_CHECK_MODULES(VINO_SERVER, glib-2.0 >= 2.12.0 gtk+-x11-2.0 gconf-2.0 libglade-2.0 libbonobo-2.0 dbus-glib-1)
	
PKG_CHECK_MODULES(VINO_CAPPLET, glib-2.0 >= 2.12.0 gtk+-2.0 >= 2.10.0 gconf-2.0 libglade-2.0 libgnomeui-2.0 dbus-glib-1)

PKG_CHECK_MODULES(VINO_TOOLS, glib-2.0 >= 2.12.0 gconf-2.0 gobject-2.0 >= 2.12.0 gnome-keyring-1)

dnl --enable-libnotify=(yes|no|auto)
LIBNOTIFY_VERSION=0.4.4

AC_ARG_ENABLE(libnotify,
              AC_HELP_STRING([--enable-libnotify],
                             [use libnotify to display a notification bubble [default=auto]]),,
              enable_libnotify=auto)

if test "x$enable_libnotify" = "xno"; then
    have_libnotify=no
else
    if test "x$enable_libnotify" = "xyes"; then
        PKG_CHECK_MODULES(VINO_LIBNOTIFY, libnotify >= $LIBNOTIFY_VERSION)
        have_libnotify=yes    
    else
        PKG_CHECK_MODULES(VINO_LIBNOTIFY,
                          libnotify >= $LIBNOTIFY_VERSION,
                          have_libnotify=yes, have_libnotify=no)
    fi;
fi

if test "x$have_libnotify" = "xyes" ; then
    AC_DEFINE(VINO_ENABLE_LIBNOTIFY, [], [Set if we should use libnotify])
fi

dnl --enable-gnome-keyring=(yes|no)
AC_ARG_ENABLE(gnome-keyring,
              AC_HELP_STRING([--enable-gnome-keyring],
                             [use gnome keyring for storing password [default=no]]),,
              enable_gnome_keyring=no)
if test "x$enable_gnome_keyring" = "xyes"; then
    PKG_CHECK_MODULES(VINO_KEYRING,
                      gnome-keyring-1,
                      AC_DEFINE(VINO_ENABLE_KEYRING, [], [Set if we should use gnome-keyring]))
fi

dnl --enable-session-support=(yes|no)
AC_ARG_ENABLE(session_support,
	      [  --enable-session-support=[no/yes] build session management utility program [default=no]],,
	      enable_session_support=no)
if test "$enable_session_support" = "yes"; then
  PKG_CHECK_MODULES(VINO_SESSION, gconf-2.0 libgnomeui-2.0)
fi
AM_CONDITIONAL(SESSION_SUPPORT, [test "$enable_session_support" = "yes"])

dnl --enable-http-server=(yes|no)
AC_ARG_ENABLE(http_server,
	      [  --enable-http-server=[no/yes] enable an HTTP server for serving a Java applet client [default=no]],,
	      enable_http_server=no)
if test "$enable_http_server" = "yes"; then
  VINO_HTTP_CFLAGS="-DVINO_ENABLE_HTTP_SERVER"
else
  VINO_HTTP_CFLAGS=""
fi
AC_SUBST(VINO_HTTP_CFLAGS)
AM_CONDITIONAL(HTTP_SERVER, [test "$enable_http_server" = "yes"])

#
# If Pango included the shared library dependencies from X11 in
# the pkg-config output, then we use that (to avoid duplicates).
# but if they were omitted to avoid binary compatibility problems
# then we need to repeat the checks.
#
if $PKG_CONFIG --exists pangoxft ; then
  PANGO_PACKAGES="pangox pangoxft"
else
  PANGO_PACKAGES="pangox"
fi

x_libs="`$PKG_CONFIG --libs $PANGO_PACKAGES`"
case x_libs in
  *-lX11*) pango_omitted_x_deps=no ;;
  *)       pango_omitted_x_deps=yes ;;
esac

if test $pango_omitted_x_deps = yes ; then
  AC_PATH_XTRA
  
  if test x$no_x = xyes ; then
    AC_MSG_ERROR([X development libraries not found])
  else
    X_LIBS="$X_PRE_LIBS $X_LIBS -lX11 $X_EXTRA_LIBS"
  fi
fi

AC_SUBST(X_LIBS)

#
# Bonobo foo
#

ORBIT_IDL="`$PKG_CONFIG --variable=orbit_idl ORBit-2.0`"
AC_SUBST(ORBIT_IDL)

BONOBO_IDLDIR="`$PKG_CONFIG --variable=idldir bonobo-activation-2.0`"
AC_SUBST(BONOBO_IDLDIR)

#
# Check for gnutls
#
AC_ARG_ENABLE(gnutls,
	      [  --disable-gnutls=[no/yes] don't build VNC over SSL support [default=no]],,
	      enable_gnutls=yes)
if test "$enable_gnutls" = "yes"; then
  AM_PATH_LIBGNUTLS(1.0.0,
	            [AC_DEFINE(HAVE_GNUTLS, 1, [Defined if the GNU TLS library is present])],
                    AC_MSG_WARN([[
***
*** libgnutls was not found. You may want to get it from
*** ftp://ftp.gnutls.org/pub/gnutls/
]]))
fi

#
# Check for gcrypt
#
AC_ARG_ENABLE(gcrypt,
	      [  --disable-gcrypt=[no/yes] don't use libgcrypt for random number generation [default=no]],,
	      enable_gcrypt=yes)
if test "$enable_gcrypt" = "yes"; then
  AM_PATH_LIBGCRYPT(1.1.90,
                    [AC_DEFINE(HAVE_GCRYPT, 1, [Defined if the libgcrypt library is present])],
                    AC_MSG_WARN([[
***
*** libgcrypt was not found. You may want to get it from
*** ftp://ftp.gnupg.org/pub/gcrypt/alpha/libgcrypt/
]]))
fi

#
# Check for XDAMAGE extension
#
XSHM_LIBS=
AC_CHECK_HEADER(X11/extensions/Xdamage.h, [
    AC_CHECK_LIB(Xdamage, XDamageQueryExtension, [
      AC_DEFINE(HAVE_XDAMAGE, 1, [Defined if the DAMAGE X extension is present])
      XDAMAGE_LIBS="-lXdamage -lXfixes"],, $X_LIBS)
  ],, [#include <X11/Xlib.h>])
AC_SUBST(XDAMAGE_LIBS)

#
# Check for MIT-SHM extension
#
XSHM_LIBS=
AC_CHECK_HEADER(X11/extensions/XShm.h, [
    AC_CHECK_LIB(Xext, XShmQueryExtension, [
      AC_DEFINE(HAVE_XSHM, 1, [Defined if the MIT-SHM X extension is present])
      XSHM_LIBS="-lXext"],, $X_LIBS)
  ],, [#include <X11/Xlib.h>])
AC_SUBST(XSHM_LIBS)

#
# Check for XTest extension
#
XTEST_LIBS=
AC_CHECK_HEADER(X11/extensions/XTest.h, [
    AC_CHECK_LIB(Xtst, XTestQueryExtension, [
      AC_DEFINE(HAVE_XTEST, 1, [Defined if the XTEST X extension is present])
      XTEST_LIBS="-lXtst"],, $X_LIBS)
  ])
AC_SUBST(XTEST_LIBS)

#
# Check for XKB extension
#
AC_CHECK_HEADER(X11/XKBlib.h, [
    AC_CHECK_LIB(X11, XkbQueryExtension,
      [AC_DEFINE(HAVE_XKB, 1, [Defined if the XKB X extension is present])]
      ,, $X_LIBS)
  ])

#
# Check for Avahi
#
AC_ARG_ENABLE(avahi,
        AS_HELP_STRING([--enable-avahi],[Enable use of avahi]),
        [case "${enableval}" in
                yes) VINO_ENABLE_MDNS=yes ;;
                no)  VINO_ENABLE_MDNS=no ;;
                *) AC_MSG_ERROR(bad value ${enableval} for --enable-avahi) ;;
        esac],
        [VINO_ENABLE_MDNS=no])

AVAHI_CFLAGS=
AVAHI_LIBS=
if test "x$VINO_ENABLE_MDNS" = "xyes" ; then
    PKG_CHECK_MODULES(AVAHI, [ avahi-client >= 0.6  avahi-glib >= 0.6 ])
    AVAHI_CFLAGS="$AVAHI_CFLAGS -DVINO_HAVE_AVAHI"
fi
AC_SUBST(AVAHI_CFLAGS)
AC_SUBST(AVAHI_LIBS)

dnl
dnl From libvncserver
dnl

AC_ARG_WITH(jpeg,
        [  --without-jpeg                       disable support for jpeg],
        , [ with_jpeg=yes ])
if test "x$with_jpeg" = "xyes"; then
        AC_CHECK_HEADER(jpeglib.h, HAVE_JPEGLIB_H="true")
fi

AC_ARG_WITH(zlib,
        [  --without-zlib                       disable support for deflate],
        , [ with_zlib=yes ])
AC_ARG_WITH(libz,
        [  --without-libz                       disable support for deflate],
        , [ with_libz=yes ])
if test "x$with_zlib" = "xyes" -a "x$with_libz" = "xyes"; then
        AC_CHECK_HEADER(zlib.h, HAVE_ZLIB_H="true")
fi

if test ! -z "$HAVE_ZLIB_H"; then
        AC_CHECK_LIB(z, deflate, , HAVE_ZLIB_H="")
        if test ! -z "$HAVE_JPEGLIB_H" -a ! -z "$HAVE_ZLIB_H"; then
                AC_CHECK_LIB(jpeg, jpeg_CreateCompress)
        fi
fi

AC_SUBST(LIBZ)
AC_SUBST(LIBJPEG)

AC_CHECK_HEADERS([netinet/in.h sys/time.h fcntl.h unistd.h sys/socket.h signal.h])
AC_CHECK_FUNCS([gettimeofday])

dnl
dnl End of libvncserver stuff
dnl


dnl
dnl Check for IPv6 support
dnl
AC_MSG_CHECKING([checking for IPv6 support])
AC_ARG_ENABLE(ipv6,
              AC_HELP_STRING([--enable-ipv6],
                             [Enable ipv6 support [default=yes]]),,
              enable_ipv6=yes)
if test "x$enable_ipv6" = "xyes"; then
    AC_TRY_COMPILE([
      #include <sys/types.h>
      #include <sys/socket.h>
    ],[
      socket(AF_INET6, SOCK_STREAM, 0);
    ], have_ipv6=yes, have_ipv6=no)

    if test "x$have_ipv6" = "xyes"; then
       AC_DEFINE(ENABLE_IPV6, [], [Define to enable IPv6 support])
    fi
else
    have_ipv6=no
fi
AC_MSG_RESULT($have_ipv6)

dnl --enable-debug=(yes|minimum|no)
AC_ARG_ENABLE(debug, [  --enable-debug=[no/yes] turn on debugging [default=no]],,enable_debug=minimum)
if test "$enable_debug" = "yes"; then
  VINO_DEBUG_CFLAGS="-DG_ENABLE_DEBUG"
else
  if test "x$enable_debug" = "xno"; then
    VINO_DEBUG_CFLAGS="-DG_DISABLE_ASSERT -DG_DISABLE_CHECKS"
  else
    VINO_DEBUG_CFLAGS=""
  fi
fi
AC_SUBST(VINO_DEBUG_CFLAGS)

AM_GCONF_SOURCE_2

dnl define a MAINT-like variable REBUILD which is set
dnl if Perl, so autogenerated sources can be rebuilt
AC_PATH_PROGS(PERL, perl5 perl)
AC_ARG_ENABLE(rebuilds, [  --disable-rebuilds      disable all source autogeneration rules],,enable_rebuilds=yes)
REBUILD=\#
if test "x$enable_rebuilds" = "xyes" && \
     test -n "$PERL" && \
     $PERL -e 'exit !($] >= 5.002)' > /dev/null 2>&1 ; then
  REBUILD=
fi
AC_SUBST(REBUILD)

AC_CONFIG_FILES([
Makefile
server/Makefile
server/libvncserver/Makefile
server/libvncserver/rfb/Makefile
capplet/Makefile
session/Makefile
tools/Makefile
docs/Makefile
po/Makefile.in
])

AC_OUTPUT([
capplet/vino-preferences.desktop.in
])

echo "
Configure summary:

	Compiler ...................:  ${CC} 
	Compiler Flags .............:  ${CFLAGS}
	Prefix......................:  ${prefix}

	Avahi support ..............:  ${VINO_ENABLE_MDNS}
	Notify support .............:  ${have_libnotify}
	GNOME Keyring support.......:  ${enable_gnome_keyring}
	Session support.............:  ${enable_session_support}
	HTTP server.................:  ${enable_http_server}
	DEBUG messages..............:  ${enable_debug}
"
